#
# dotfiles config
#

# shortcut to this dotfiles path is $DOTFILES
# get this script's parent dir
export DOTFILES=~/dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/dev

# antigen - plugin manager
source ~/antigen/antigen.zsh

antigen use oh-my-zsh

antigen bundles <<EOBUNDLES
  git
  heroku
  command-not-found

#  nicoulaj's moar completion files for zsh
  zsh-users/zsh-completions src

  zsh-users/zsh-syntax-highlighting
  zsh-users/zsh-history-substring-search
EOBUNDLES

# Tracks your most used directories, based on 'frecency'. And its accompanying
# setup code.
antigen bundle rupa/z
add-zsh-hook precmd _z_precmd
function _z_precmd {
  _z --add "$PWD"
}

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

antigen apply
antigen theme zhann

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  if [[ ! -f `dirname $file`/.disable ]]
  then
    source $file
  fi
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  if [[ ! -f `dirname $file`/.disable ]]
  then
    source $file
  fi
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  if [[ ! -f `dirname $file`/.disable ]]
  then
    source $file
  fi
done

unset config_files

